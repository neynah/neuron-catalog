Meteor.startup ->
  if NeuronCatalogConfig.find().count() is 0
    doc =
      _id: "config"
      project_name: "neuron catalog"
      data_authors: "authors"
      blurb: ""
    NeuronCatalogConfig.insert doc
  return

# ----------------------------------------
Meteor.publish "neuron_catalog_config", ->
  NeuronCatalogConfig.find {}

Meteor.publish "driver_lines", ->
  DriverLines.find {}  if @userId
Meteor.publish "neuron_types", ->
  NeuronTypes.find {}  if @userId
Meteor.publish "brain_regions", ->
  BrainRegions.find {}  if @userId
Meteor.publish "binary_data", ->
  BinaryData.find {}  if @userId
Meteor.publish "simple_upload_processor_status", ->
  SimpleUploadProcessorStatus.find {} if @userId

# ----------------------------------------
#   Upload processor status
#
# We test if status document (generated by other server-side program)
# has been updated in the last 10 seconds.

update_upload_processor_status = ->
  doc = UploadProcessorStatus.findOne({'_id':'status'})
  status_is_ok = false
  if doc?
    doc_time = new Date(doc.time)
    now = Date.now()
    diff_msec = now-doc_time
    if (diff_msec < 10000)
      status_is_ok = true
  SimpleUploadProcessorStatus.update({'_id':'status'},{'$set': {status_is_ok: status_is_ok}},{upsert: true})

Meteor.setInterval(update_upload_processor_status, 5000) # check status every 5 seconds

# ----------------------------------------

Meteor.publish "userData", ->
  if @userId
    Meteor.users.find {},
      fields:
        username: 1

# ----------------------------------------

logged_in_allow =
  insert: (userId, doc) ->
    !!userId

  update: (userId, doc, fields, modifier) ->
    !!userId

  remove: (userId, doc) ->
    !!userId

DriverLines.allow logged_in_allow
BinaryData.allow logged_in_allow
NeuronTypes.allow logged_in_allow
BrainRegions.allow logged_in_allow
UploadProcessorStatus.allow logged_in_allow

NeuronCatalogConfig.allow logged_in_allow
